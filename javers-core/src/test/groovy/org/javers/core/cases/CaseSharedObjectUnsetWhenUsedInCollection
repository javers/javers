package org.javers.core.cases

import org.javers.core.JaversBuilder
import spock.lang.Specification

class CaseSharedObjectUnsetWhenUsedInCollection extends Specification {

    class ComplexProperty {
        String value
    }

    class ListItem {
        String name
        ComplexProperty complexProperty
    }

    class TopLevelClass {
        ComplexProperty complexProperty
        List<ListItem> items
    }

    def "should compare shared nested property on fields"() {
        given:
        def javers = JaversBuilder.javers().build()

        def l1 = new TopLevelClass(
                complexProperty: new ComplexProperty(value: "value1"),
                items: [
                        new ListItem(name: "name1", complexProperty: new ComplexProperty(value: "value1"))
                ])

        def complexProperty = new ComplexProperty(value: "value1")
        def l2 = new TopLevelClass(
                complexProperty: complexProperty,
                items: [
                        new ListItem(name: "name1", complexProperty: complexProperty)
                ])

        when:
        def diff = javers.compare(l1, l2)

        then:
        diff.changes.isEmpty()
    }
}
