apply plugin: 'application'
mainClassName = "org.javers.core.selftest.Application"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
dependencies {
    api "org.slf4j:slf4j-api:$slf4jApiVersion"
    api "com.google.code.gson:gson:$gsonVersion"
    implementation 'org.picocontainer:picocontainer:2.15'
    implementation "io.github.classgraph:classgraph:$classgraphVersion"

    //should be on application's classpath when using custom comparators
    optionalImplementation "com.google.guava:guava:$guavaVersion"

    testImplementation 'org.assertj:assertj-core:3.24.1'
    testImplementation "com.google.guava:guava:$guavaVersion"
    testImplementation 'jakarta.persistence:jakarta.persistence-api:3.2.0'
    testImplementation "com.google.guava:guava:$guavaVersion"
    testImplementation "org.mongodb:mongodb-driver-sync:$mongoDbDriverVersion"
    testImplementation ('org.mongodb.morphia:morphia:0.108') {
        exclude group: 'org.mongodb', module: 'mongo-java-driver'
    }
    testImplementation 'tools.jackson.core:jackson-databind:3.0.2'
    testImplementation 'org.codehaus.gpars:gpars:1.2.1'
}

task testJar(type: Jar) {
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

task fatJar(type: Jar) {
    archiveClassifier = 'all'
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
    testArtifacts
}

artifacts {
    testArtifacts testJar
}
