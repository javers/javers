dependencies {
    compile project(':javers-core')
    compile "org.mongodb:mongodb-driver-legacy:$mongoDbDriverVersion"
    compile "com.google.guava:guava:$guavaVersion"

    testCompile 'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final'
    testCompile project(path: ":javers-core", configuration: "testArtifacts")
    testCompile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$embeddedMongo"
    testCompile 'org.codehaus.gpars:gpars:1.2.1'
}

task integrationTest(type: Test) {
    filter {
        includeTestsMatching 'org.javers.repository.mongo.integration.**'
    }
    testLogging {
        exceptionFormat = 'full'
        events 'failed', 'passed'
    }
}

test {
    exclude 'org/javers/repository/mongo/integration/**'
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

configurations {
    testArtifacts
}

artifacts {
    testArtifacts testJar
}
