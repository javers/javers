buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'pl.allegro.tech.build:axion-release-plugin:0.9.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'axion-release'

scmVersion {
    tag {
        prefix = 'javers'
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'propdeps'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    project.group = 'org.javers'
    project.version = scmVersion.version

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile "org.spockframework:spock-core:$spockVersion"
        testCompile 'org.apache.commons:commons-lang3:3.3.2'
        testCompile 'ch.qos.logback:logback-classic:1.1.2'
        testCompile 'cglib:cglib-nodep:3.2.0'
        testCompile 'org.objenesis:objenesis:2.1'
        testCompile 'org.easytesting:fest-assert-core:2.0M10'
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '2.10'
    }

    javadoc {
        options.noTimestamp()
    }

    task example (dependsOn: 'cleanTest') << {
        tasks.test.testLogging {
            events 'failed', 'passed'
            showStandardStreams = true
        }
        tasks.test.execute()
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourceJar
    }

    //disable javadoc doclint for Java8
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    install.repositories.mavenInstaller.pom.project {
        name 'JaVers'

        packaging 'jar'

        description 'JaVers object auditing and diff framework'
        url 'http://javers.org'
        inceptionYear '2013'

        scm {
            connection 'scm:git:https://github.com/javers/javers.git'
            url 'https://github.com/javers/javers'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                name 'Bartosz Walacik'
            }
            developer {
                name 'Pawel Szymczyk'
            }
            developer {
                name 'Michal Wesolowski'
            }
        }
    }

    bintray {
        user = project.ext.has('bintrayUser') ? project.ext.bintrayUser : System.getenv('bintrayUser')
        key = project.ext.has('bintrayKey') ? project.ext.bintrayKey : System.getenv('bintrayKey')

        configurations = ['archives']
        publish = true
        pkg {
            repo = 'maven'
            name = 'javers'
            version {
                mavenCentralSync {
                    user = project.ext.has('sonatypeUser') ? project.ext.sonatypeUser : System.getenv('sonatypeUser')
                    password = project.ext.has('sonatypePassword') ? project.ext.sonatypePassword : System.getenv('sonatypePassword')
                    sync = true
                    close = '1'
                }
            }
        }
    }

    test {
        testLogging.exceptionFormat = 'full'
        testLogging.showStackTraces = true
    }
}